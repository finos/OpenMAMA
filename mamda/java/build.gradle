apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

def mamdaVersion = project.findProperty('mamdaVersion') ?: new File("${rootDir.absolutePath}/../../VERSION").text.trim() + "-1"

if (!project.hasProperty('legacyNaming')) {
  group = "org.openmama"
  archivesBaseName = "openmamda"
  version = mamdaVersion
}

repositories {
    mavenCentral()
}

dependencies {
    compile project(':mamajni')
    testCompile "junit:junit:4.12"  // Or whatever version
}

test {
    systemProperties = [
            dictionary: System.getProperty('dictionary')
    ]
}

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.compile
    title = 'MAMDA (Middleware Agnostic Market Data API) Java specification, v' + mamdaVersion
    options.overview = 'overview.html'
    options.windowTitle = 'MAMDA Java ' + mamdaVersion
    options.bottom = 'Copyright 2011 NYSE Technologies'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'OpenMAMDA'
        packaging 'jar'
        // optionally artifactId can be defined here 
        description 'OpenMAMDA is a high performance Market Data API written on top of OpenMAMA.'
        url 'https://www.openmama.org'

        scm {
          connection 'scm:https://github.com/OpenMAMA/OpenMAMA.git'
          developerConnection 'scm:git@github.com:OpenMAMA/OpenMAMA.git'
          url 'https://github.com/OpenMAMA/OpenMAMA'
        }

        licenses {
          license {
            name 'GNU LESSER GENERAL PUBLIC LICENSE 2.1'
            url 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.en.html'
          }
        }

        developers {
          developer {
            id 'fquinner'
            name 'Frank Quinn'
            email 'fquinn@cascadium.io'
          }
        }
      }
    }
  }
}