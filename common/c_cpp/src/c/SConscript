#!/usr/bin/python
# -*- coding: utf-8 -*-
import os

Import('*')
env = env.Clone()
includePath = []

osname = env['host']['os']

if osname == 'Linux':
    includePath.append('./linux')
    includePath.append('./linux/wombat')
    InstWomPlatformHeaders = Glob('linux/wombat/*.h')
elif osname == 'Darwin':
    includePath.append('./darwin')
    includePath.append('./darwin/wombat')
    InstWomPlatformHeaders = Glob('darwin/wombat/*.h')

includePath.append('./wombat')

srcdir = os.path.join(Dir('#').abspath, 'common/c_cpp/src/c')

InstHeaders = \
    Split("""
    destroyHandle.h
    list.h
    lookup2.h
    platform.h
    property.h
    wlock.h
    timers.h
""")

InstWomHeaders = Glob('wombat/*.h')

includePath.append('.')
includePath.append('../..')

if env['host']['os'] == 'Darwin':
    env.Append(LIBS='-ldl -lm', CPPPATH=includePath, CCFLAGS=['-fPIC'])
else:
    env.Append(LIBS='-ldl -luuid', CPPPATH=includePath, CCFLAGS=['-fPIC'])

sources = Glob('*.c')
sources.append(Glob(osname.lower() + '/*.c'))
sources.append('properties.l')

objects = []
StaticLibs['libwombatcommon'] = []

for s in sources:
    obj = env.Object(s)
    StaticLibs['libwombatcommon'].append(obj)
    objects.append(obj)

lib = env.StaticLibrary('libwombatcommon', objects)

Alias('install', env.Install('$libdir', lib))
Alias('install', env.Install('$incdir', InstHeaders))
Alias('install', env.Install('$incdir/wombat', InstWomHeaders))
Alias('install', env.Install('$incdir/wombat', InstWomPlatformHeaders))

