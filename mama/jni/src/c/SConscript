#!/usr/bin/python
# -*- coding: utf-8 -*-
Import('*')
env = env.Clone()

target = 'libmamajni'

incPath = []
incPath.append('.')
incPath.append('../../../jni/jni')
incPath.append('#common/c_cpp/src/c')
incPath.append('#mama/c_cpp/src/c')

libPath = []
libPath.append('$libdir')

env['CCFLAGS'] = [x for x in env['CCFLAGS'] if x != '-pedantic-errors']
if env['host']['os'] == 'Darwin':
    env.Append(LIBS=['mama', 'wombatcommon'], LIBPATH=libPath,
               CPPPATH=incPath)
else:
    env.Append(LIBS=['mama', 'wombatcommon', 'rt'], LIBPATH=libPath,
               CPPPATH=incPath,
               CFLAGS=(["-Wno-deprecated-declarations"] + env['CFLAGS']))

sources = \
    Split("""
    version.c
    mamajni.c
    mamaconnectionjni.c
    mamatransportjni.c
    mamabridgejni.c
    mamasubscriptionjni.c
    mamabasicsubscriptionjni.c
    mamamsgjni.c
    mamamsgfieldjni.c
    mamatimerjni.c
    mamadictionaryjni.c
    mamaqueuejni.c
    mamadispatcherjni.c
    mamapublisherjni.c
    mamadqpublisherjni.c
    mamadqpublishermanagerjni.c
    mamainboxjni.c
    mamajniutils.c
    mamapricejni.c
    mamadatetimejni.c
    mamasourcejni.c
    mamaftmemberjni.c
    subscriptioncommon.c
    mamaloghandler.c
    mamapayloadbridgejni.c
""")

lib = []
lib.append(env.SharedLibrary(target, sources))
lib.append(env.StaticLibrary(target, [ sources, StaticLibs['libwombatcommon'] ] ))

Alias('install', env.Install('$libdir', lib))
