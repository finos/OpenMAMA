#!/usr/bin/python
# -*- coding: utf-8 -*-
import os
Import('*')
env = env.Clone()

target = 'libmamaqpidimpl'

env.Append( CPPDEFINES 	= ['BRIDGE', 'MAMA_DLL', 'HAVE_QPID_PROTON_VERSION_H'] )

libPath = []
libPath.append('$libdir')

incPath = []
incPath.append('$libevent_home/WIN32-Code')
incPath.append('$libevent_home')
incPath.append('$qpid_home/proton-c/include')
incPath.append('#common/c_cpp/src/c/windows')
incPath.append('#common/c_cpp/src/c')
incPath.append('#mama/c_cpp/src/c')

libs = []
libs.append('libwombatcommon%s.lib' % ( env['suffix'] ))
libs.append('libmamac%s.lib' % ( env['suffix'] ))

if env['target_arch'] == 'x86_64':
  archPath = "x64"
else:
  archPath = ""

libs.append('libevent')

if 'debug' in env['build']:
  libs.append('qpid-protond')
  buildType = "Debug"
else:
  libs.append('qpid-proton')
  buildType = "Release"

libPath.append('%s/proton-c/%s/%s' % (env['qpid_home'], archPath, buildType))
libPath.append('%s/WIN32-Prj/%s/%s' % (env['libevent_home'], archPath, buildType))

env['CCFLAGS'].append(['/TP', '/WX-'])
env['CPPPATH'] = incPath
env.Append(LIBS = libs, LIBPATH=libPath) 

sources = Glob('*.c')

env.InstallLibrary(sources, target)
